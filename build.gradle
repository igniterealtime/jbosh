apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'eclipse'
apply plugin: 'osgi'
apply plugin: 'signing'
apply plugin: 'checkstyle'

apply from: 'version.gradle'

ext {
	gitCommit = getGitCommit()
	sonatypeCredentialsAvailable = project.hasProperty('sonatypeUsername') && project.hasProperty('sonatypePassword')
	isReleaseVersion = !isSnapshot
	isContinuousIntegrationEnvironment = Boolean.parseBoolean(System.getenv('CI'))
	signingRequired = !(isSnapshot || isContinuousIntegrationEnvironment)
	sonatypeSnapshotUrl = 'https://oss.sonatype.org/content/repositories/snapshots'
	sonatypeStagingUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
	buildDate = (new java.text.SimpleDateFormat("yyyy-MM-dd")).format(new Date())
	androidBootClasspath = getAndroidRuntimeJar()
}

group = 'org.igniterealtime.jbosh'
sourceCompatibility = 1.7
version = shortVersion
if (isSnapshot) {
	version += '-SNAPSHOT'
}

description = """\
JBOSH ${version}
XEP-0124: Bidirectional-streams Over Synchronous HTTP (BOSH)
"""

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

task testSourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'test-sources'
	from sourceSets.test.allSource
}

artifacts {
	archives sourcesJar
	archives javadocJar
	archives testSourcesJar
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
			artifact sourcesJar
			artifact javadocJar
			artifact testSourcesJar
			pom {
				name = 'JBOSH'
				description = 'XEP-0124: Bidirectional-streams Over Synchronous HTTP (BOSH)'
				packaging = 'jar'
				url = 'http://www.igniterealtime.org/projects/jbosh/'

				scm {
					url = 'https://github.com/igniterealtime/jbosh'
					connection = 'scm:git:https://github.com/igniterealtime/jbosh.git'
					developerConnection = 'scm:git:https://github.com/igniterealtime/jbosh.git'
				}

				licenses {
					license {
						name = 'The Apache Software License, Version 2.0'
						url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
						distribution = 'repo'
					}
				}

				developers {
					developer {
						id = 'flow'
						name = 'Florian Schmaus'
						email = 'flow@igniterealtime.org'
					}
				}
			}
		}
	}
	repositories {
		maven {
			url isSnapshot ? sonatypeSnapshotUrl : sonatypeStagingUrl
			if (sonatypeCredentialsAvailable) {
				credentials {
					username = sonatypeUsername
					password = sonatypePassword
				}
			}
		}
	}
}

repositories {
	mavenCentral()
	maven {
		url sonatypeSnapshotUrl
	}
}

signing {
	useGpgCmd()
	required { signingRequired }
	sign publishing.publications.mavenJava
}

dependencies {
	compile 'xpp3:xpp3:1.1.4c'
	compile 'org.apache.httpcomponents:httpclient:4.3.3'
	testCompile 'org.xlightweb:xlightweb:2.5'
	testCompile 'junit:junit:4.12'
}

tasks.withType(JavaCompile) {
		// Some systems may not have set their platform default
		// converter to 'utf8', but we use unicode in our source
		// files. Therefore ensure that javac uses unicode
		options.encoding = "utf8"
		options.compilerArgs = [
			'-Xlint:all',
			// Set '-options' because a non-java7 javac will emit a
			// warning if source/traget is set to 1.7 and
			// bootclasspath is *not* set.
			// TODO implement a sound heuristic to determine a java7
			// rt.jar on the build host. And if none is found,
			// fallback to using a environment variable,
			// e.g. JAVA7_HOME. See SMACK-651.
			'-Xlint:-options',
			'-Werror',
		]
}

task compileAndroid(type: JavaCompile) {
	source = compileJava.source
	classpath = compileJava.classpath
	destinationDir = new File(buildDir, 'android')
	options.bootstrapClasspath = files(androidBootClasspath)
}

test { dependsOn compileAndroid }

def getGitCommit() {
	def dotGit = new File("$projectDir/.git")
	if (!dotGit.isDirectory()) return 'non-git build'

	def cmd = 'git describe --tags --dirty=+'
	def proc = cmd.execute()
	def gitCommit = proc.text.trim()
	assert !gitCommit.isEmpty()
	gitCommit
}

def getAndroidRuntimeJar() {
	def androidHome = getAndroidHome()
	def androidJar = new File("$androidHome/platforms/android-$smackMinAndroidSdk/android.jar")
	if (androidJar.isFile()) {
		return androidJar
	} else {
		throw new Exception("Can't find android.jar for $smackMinAndroidSdk API. Please install corresponding SDK platform package")
	}
}

// Currently not used by jbosh
def getAndroidJavadocOffline() {
	def androidHome = getAndroidHome()
	return androidHome.toString() + "/docs/reference"
}

def getAndroidHome() {
	def androidHomeEnv = System.getenv("ANDROID_HOME")
	if (androidHomeEnv == null) {
		throw new Exception("ANDROID_HOME environment variable is not set")
	}
	def androidHome = new File(androidHomeEnv)
	if (!androidHome.isDirectory()) throw new Exception("Environment variable ANDROID_HOME is not pointing to a directory")
	return androidHome
}
